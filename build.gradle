plugins {
	id "com.jfrog.artifactory" version "4.4.13"
	id 'java'
	id 'maven'
}

group = 'io.spring.asciidoctor'
description = 'Spring Asciidoctor Extensions'

apply from: 'gradle/publish-maven.gradle'

repositories {
	mavenCentral()
}

ext {
	asciidoctorjVersion = '1.5.4.1'
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
	compileOnly "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
	testCompile "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
	testCompile 'junit:junit:4.12'
	testCompile 'org.assertj:assertj-core:2.6.0'
}

javadoc {
	title = "$project.description $project.version API"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = "javadoc"
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

artifactory {
	contextUrl = 'https://repo.spring.io'
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			if (project.hasProperty('artifactoryUsername')
					&& project.hasProperty('artifactoryPassword')) {
				username = artifactoryUsername
				password = artifactoryPassword
			}
		}
	}
}

artifactoryPublish {
	publishConfigs 'archives'
	publishIvy false
}